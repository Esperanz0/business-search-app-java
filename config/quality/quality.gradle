/*
 * Copyright 2017 Vandolf Estrellado
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Checks quality and style of Java code.
 */

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

ext.configDir = "$rootDir/config/quality"
ext.reportsDir = "$buildDir/reports"

// CHECK QUALITY TASK
check.dependsOn 'checkQuality'

class CheckQuality extends DefaultTask {}

task checkQuality(type: CheckQuality) {
    group = "Static Analysis"
    description = "Runs all static analysis checks on all build variants."
}

def generateCheckQualityTask(String buildVariant, boolean withLint) {
    buildVariant = buildVariant.capitalize();

    def checkQualityVariant = "check${buildVariant}Quality";
    def checkQualityVariantTaskDependencies = ['checkstyle', 'pmd', "findbugs${buildVariant}"]
    if (withLint) {
        checkQualityVariantTaskDependencies += "lint${buildVariant}"
    }

    task(checkQualityVariant, type: CheckQuality, dependsOn: checkQualityVariantTaskDependencies) {
        group = "Static Analysis"
        description = "Runs all static analysis checks on the ${buildVariant} build variant."
    }

    checkQuality.dependsOn "${checkQualityVariant}"
}

def generateCheckQualityTaskForVariant(variant) {
    generateCheckQualityTask(variant.name, true)
}

// CHECKSTYLE TODO flavorize
task checkstyle(type: Checkstyle) {
    group = "Static Analysis"
    description = "Checks style of source code"

    configFile file("$configDir/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("$configDir/checkstyle/suppressions.xml").absolutePath
    configProperties.checkstyleJavaHeaderPath = file("$configDir/checkstyle/java.header").absolutePath
    configProperties.checkstyleXmlHeaderPath = file("$configDir/checkstyle/xml.header").absolutePath
    source 'src/main', 'src/test'
    include '**/*.java'
    include '**/*.xml'
    exclude '**/gen/**'
    classpath = files()
}

// FINDBUGS
task findbugs(type: FindBugs) {
    group = "Static Analysis"
    description = "Finds bugs in bytecode on all build variants."
}

// Findbugs requires the binary to be assembled first, which takes time. This is why it should
// be broken up into separate tasks per build variant to give the option to avoid executing
// this for all build variants
def generateFindbugsTask(String flavor, String buildType, String buildVariant) {
    def findBugsDependency = buildType.isEmpty() ? "assemble" : "assemble${buildVariant.capitalize()}";
    def findBugsVariant = "findbugs${buildVariant.capitalize()}";

    task("${findBugsVariant}", type: FindBugs, dependsOn: "${findBugsDependency}", overwrite: true) {
        group = "Static Analysis"
        description = "Finds bugs in bytecode on the ${buildVariant} build variant."

        ignoreFailures = false
        effort = "max"
        reportLevel = "high"
        excludeFilter = new File("$configDir/findbugs/findbugs-filter.xml")

        def classesDir;
        if (!buildType.isEmpty() && !flavor.isEmpty()) {
            classesDir = "${buildDir}/intermediates/classes/${flavor}/${buildType}"
        } else if (!buildType.isEmpty()) {
            classesDir = "${buildDir}/intermediates/classes/${buildType}"
        } else {
            classesDir = "${buildDir}/classes/${buildVariant}"
        }

        classes = files(classesDir)
        classpath = files(classesDir)
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = true
            html.enabled = false
            xml {
                destination "$reportsDir/findbugs/${buildVariant}/findbugs.xml"
            }
            html {
                destination "$reportsDir/findbugs/${buildVariant}/findbugs.html"
            }
        }
    }

    tasks.findbugs.dependsOn "${findBugsVariant}"
}

def generateFindbugsTaskForVariant(variant) {
    def flavor = variant.flavorName
    def buildType = variant.buildType.name
    def buildVariant = flavor + (flavor.isEmpty() ? buildType : buildType.capitalize())
    generateFindbugsTask(flavor, buildType, buildVariant)
}

// PMD TODO flavorize
task pmd(type: Pmd) {
    group = "Static Analysis"
    description = "Finds bugs in source code on all build variants"

    ignoreFailures = false
    ruleSetFiles = files("$configDir/pmd/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$reportsDir/pmd/pmd.xml"
        }
        html {
            destination "$reportsDir/pmd/pmd.html"
        }
    }
}

ext {
    generateCheckQualityTask = this.&generateCheckQualityTask
    generateCheckQualityTaskForVariant = this.&generateCheckQualityTaskForVariant
    generateFindbugsTask = this.&generateFindbugsTask
    generateFindbugsTaskForVariant = this.&generateFindbugsTaskForVariant
}